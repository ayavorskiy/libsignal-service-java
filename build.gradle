buildscript {
    ext.kotlin_version = '1.7.20'
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
            content {
                includeGroupByRegex "org\\.jlleitschuh\\.gradle.*"
            }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.19'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.2.1"
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

project.group = "com.github.ayavorskiy"

def getRepositoryUsername() {
    return hasProperty('whisperSonatypeUsername') ? whisperSonatypeUsername : ""
}

def getRepositoryPassword() {
    return hasProperty('whisperSonatypePassword') ? whisperSonatypePassword : ""
}

nexusPublishing {
    repositories {
        sonatype {
            username = getRepositoryUsername()
            password = getRepositoryPassword()
        }
    }
}

subprojects {
    ext.lib_signal_service_version_number = "2.15.3_unofficial_65"
    ext.lib_signal_service_group_info     = "com.github.ayavorskiy"

    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }
}
